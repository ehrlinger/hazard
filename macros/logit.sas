*******************************************************************************;
* <2003-10-17>                                                                 ;
* Decile Analysis of a Continuous Variable With Respect to An Event            ;
  %MACRO LOGIT(VAR=, EVENT=EVENT, GROUPS=10, MEAN=MEAN, MAX=MAX,
               MIN=MIN, PROB=PROB, N=N, SUM=SUM, LOGIT=LOGIT, IN=,
               OUT=LOGIT, GOMPIT=0, COX=0, INTERVAL=);
*______________________________________________________________________________;
*                                                                              ;
* DEFINITIONS OF CALLING ARGUMENTS:            DEFAULT:                        ;
*                                                                              ;
*   EVENT    = Event (0/1) indicator             EVENT                         ;
*   VAR      = Variable to be categorized                                      ;
*   GROUPS   = Number of groups (deciles)        10                            ;
*   MEAN     = Mean within each group of VAR     MEAN                          ;
*   MAX      = Max within each group of VAR      MAX                           ;
*   MIN      = Min within each group of VAR      MIN                           ;
*   PROB     = Group probability of event        PROB                          ;
*   LOGIT    = Logit for each group              PROB                          ;
*   N        = Number of observations in decile  N                             ;
*   SUM      = Number of events in decile        EVENTS                        ;
*   IN       = Input data set name                                             ;
*   OUT      = Output data set name              LOGIT                         ;
*   GOMPIT   = Use Gompertz rather than logistic 0 (1 for GOMPIT)              ;
*   COX      = Use Cox transform (const. hazard) 0 (1 for COX)                 ;
*   INTERVAL = Interval to event (Cox method)    Not give                      ;
*                                                                              ;
* NOTE:  The output data set consists of GROUP observations from which         ;
*        various plots and relations can be run.                               ;
*______________________________________________________________________________;
*                                                                              ;
* Refine data set                                                              ;
  DATA &OUT; SET &IN;
  OPTIONS NONOTES;
  %IF &COX=1 %THEN %DO;
    KEEP &VAR &EVENT &INTERVAL;
  %END;
  %ELSE %DO;
    KEEP &VAR &EVENT;
  %END;

*******************************************************************************;
* Form groups                                                                  ;
  IF &VAR=. THEN DELETE;
  %IF &COX=1 %THEN %DO;  IF &INTERVAL=. THEN DELETE; %END;
  PROC RANK DATA=&OUT OUT=&OUT GROUP=&GROUPS TIES=MEAN;
       VAR &VAR; RANKS _DECILE_;
*******************************************************************************;
* Sumarize groups for all events                                               ;
  %IF &COX=0 %THEN %DO;
    PROC SUMMARY DATA=&OUT; CLASS _DECILE_; VAR &EVENT &VAR;
         OUTPUT OUT=&OUT MEAN=&PROB &MEAN N(&EVENT)=&N SUM(&EVENT)=&SUM
         MAX(&VAR)=&MAX MIN(&VAR)=&MIN;
  %END;
  %ELSE %DO;
    PROC SUMMARY DATA=&OUT; CLASS _DECILE_; VAR &EVENT &VAR &INTERVAL;
         OUTPUT OUT=&OUT MEAN(&EVENT &VAR)=&PROB &MEAN N(&EVENT)=&N SUM(&EVENT
         &INTERVAL)=&SUM SUM_INT MAX(&VAR)=&MAX MIN(&VAR)=&MIN;
  %END;
*******************************************************************************;
* Specify tranform:                                                            ;
*   Logistic:  Ln[P/(1-P)]                                                     ;
*   Gompertz:  Ln(-Ln[P])                                                      ;
*   Cox:       Ln[E/Sum(T)] (proportional constant hazards)                    ;
  DATA &OUT; SET &OUT;
  IF _TYPE_;
  %IF &COX=0 %THEN %DO;
    Q=1-&PROB;
    IF &GOMPIT=0 THEN &LOGIT=LOG(&PROB/Q);
    ELSE &LOGIT=LOG(-LOG(Q));
  %END;
  %ELSE %DO; &LOGIT=LOG(&SUM/SUM_INT); %END;
  KEEP &MEAN &N &SUM &MAX &MIN &PROB &LOGIT;
  OPTIONS NOTES;
*******************************************************************************;
  %MEND LOGIT;
*******************************************************************************;
